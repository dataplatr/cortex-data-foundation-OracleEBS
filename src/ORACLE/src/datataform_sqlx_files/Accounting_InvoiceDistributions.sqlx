---------------------------------------------------------------------
-- Edw.Accounting_InvoiceDistributions.sqlx Loading script using Dataform
---------------------------------------------------------------------
config {
  type: "table",
  schema: "Edw",
  tags: ["Procurement"],
  assertions:{
    uniqueKey: ["InvoiceDistributionId"],
    nonNull: ["InvoiceDistributionId"]
  }
}

with inv_pmt as
(
select
pmt.INVOICE_ID,
date(chk.CHECK_DATE) as LastPaymentDate,
chk.CHECK_NUMBER as CheckNumber, 
STATUS_LOOKUP_CODE as CheckPaymentStatus

from ${ref("OracleEbs-AP_INVOICE_PAYMENTS_ALL")} as pmt
join ${ref("OracleEbs-AP_CHECKS_ALL")} chk
on pmt.CHECK_ID = chk.CHECK_ID and chk.InactiveInd=false
qualify row_number () over (partition by pmt.INVOICE_ID order by chk.CHECK_DATE desc) = 1
),
POD as (
select 
he.PO_HEADER_ID as POHeaderId,
--he.attribute3 as CapexCode,
lines.LINE_NUM,
dists.PO_DISTRIBUTION_ID,
he.SEGMENT1 as PONumber,
he.CREATION_DATE as POCreationDate, 
rates.CONVERSION_RATE as POConversionRate,
(lineloc.QUANTITY - lineloc.QUANTITY_CANCELLED)*lineloc.PRICE_OVERRIDE as POLineAmount

from  ${ref("OracleEbs-PO_HEADERS_ALL")} as he 
inner join ${ref("OracleEbs-PO_LINES_ALL")} as lines 
	      on   he.PO_HEADER_ID  =   lines.PO_HEADER_ID and lines.InactiveInd=false
inner join ${ref("OracleEbs-PO_LINE_LOCATIONS_ALL")} as lineloc
	      on  lines.PO_LINE_ID     =   lineloc.PO_LINE_ID	 and lineloc.InactiveInd=false
inner join ${ref("OracleEbs-PO_DISTRIBUTIONS_ALL")} as dists
	      on  lineloc.LINE_LOCATION_ID     = 	dists.LINE_LOCATION_ID and dists.InactiveInd=false
inner join ${ref("OracleEbs-GL_LEDGERS")} as gll
        on dists.SET_OF_BOOKS_ID = gll.LEDGER_ID and gll.InactiveInd=false
left join ${ref("OracleEbs-GL_DAILY_RATES")} as rates
on date(rates.CONVERSION_DATE) = date(he.CREATION_DATE) and rates.FROM_CURRENCY = gll.CURRENCY_CODE
and rates.CONVERSION_TYPE = 'Spot' and rates.TO_CURRENCY ='USD' and rates.InactiveInd=false
qualify row_number () over (partition by lineloc.LINE_LOCATION_ID order by dists.PO_DISTRIBUTION_ID) = 1
),
POH as (
select 
POD.POHeaderId,
POD.PONumber,
--POD.CapexCode,
POD.POCreationDate, 
POD.POConversionRate,
sum(POD.POLineAmount) as POHeaderAmount
from POD
group by  POD.POHeaderId,POD.PONumber,POD.POCreationDate, POD.POConversionRate
),
hold as 
(
select
 hd.INVOICE_ID,
 count(hd.INVOICE_ID) TotalCountHold,
 count(hd.RELEASE_LOOKUP_CODE) CountofReleaseCode,
 String_Agg(distinct (case when RELEASE_REASON IS NULL then HOLD_LOOKUP_CODE end),',') HoldLookupCode
from ${ref("OracleEbs-AP_HOLDS_ALL")}  hd
where hd.InactiveInd = false
group by  INVOICE_ID
)

select
inv.ParentSystemId,
inv.SystemId,
inv.VENDOR_ID  as  VendorId,
inv.INVOICE_ID  as  InvoiceId,
inv.QUICK_PO_HEADER_ID  as  QuickPOHeaderId,
inv.PAYMENT_METHOD_CODE  as  PaymentMethodCode,
inv.LEGAL_ENTITY_ID  as  LegalEntityId,
inv.TAXATION_COUNTRY  as  TaxationCountry,
inv.SELF_ASSESSED_TAX_AMOUNT  as  SelfAssessedTaxAmount,
inv.TOTAL_TAX_AMOUNT  as  TotalTaxAmount,
inv.AWARD_ID  as  AwardId,
inv.GL_DATE  as  GlDate,
inv.PAYMENT_CROSS_RATE_DATE  as  PaymentCrossRateDate,
inv.PAYMENT_CROSS_RATE_TYPE  as  PaymentCrossRateType,
inv.AUTO_TAX_CALC_FLAG  as  AutoTaxCalculationFlag,
inv.ORG_ID  as  OrganisationId,
inv.DOC_CATEGORY_CODE  as  DocumentCategoryCode,
inv.DOC_SEQUENCE_VALUE  as  DocumentSequenceValue,
inv.DOC_SEQUENCE_ID  as  DocumentSequenceId,
inv.EXCLUDE_FREIGHT_FROM_DISCOUNT  as  ExcludeFreightFromDiscount,
inv.DISC_IS_INV_LESS_TAX_FLAG  as  DiscountIsInvoiceLessTaxFlag,
inv.EXCHANGE_DATE  as  ExchangeDate,
inv.EXCHANGE_RATE_TYPE  as  ExchangeRateType,
inv.EXCHANGE_RATE  as  ExchangeRate,
inv.VOUCHER_NUM  as  VoucherNumber,
inv.INVOICE_RECEIVED_DATE  as  InvoiceReceivedDate,
inv.GOODS_RECEIVED_DATE  as  GoodsReceivedDate,
inv.FREIGHT_AMOUNT  as  FreightAmount,
inv.PAYMENT_STATUS_FLAG  as  PaymentStatusFlag,
inv.ACCTS_PAY_CODE_COMBINATION_ID  as  AccountsPayCodeCombinationId,
inv.PAY_GROUP_LOOKUP_CODE  as  PayGroupLookupCode,
inv.PAYMENT_METHOD_LOOKUP_CODE  as  PaymentMethodLookupCode,
inv.TERMS_DATE  as  TermsDate,
inv.TERMS_ID  as  TermsId,
inv.AMOUNT_APPLICABLE_TO_DISCOUNT  as  AmountApplicableToDiscount,
inv.BATCH_ID  as  BatchId,
inv.DESCRIPTION  as  InvoiceDescription,
inv.INVOICE_TYPE_LOOKUP_CODE  as  InvoiceTypeLookupCode,
inv.SOURCE  as  Source,
inv.INVOICE_DATE  as  InvoiceDate,
inv.CANCELLED_DATE  as  CancelledDate,
inv.DISCOUNT_AMOUNT_TAKEN  as  DiscountAmountTaken,
inv.AMOUNT_PAID  as  AmountPaid,
inv.VENDOR_SITE_ID  as  VendorSiteId,
inv.INVOICE_AMOUNT  as  InvoiceAmount,
inv.PAYMENT_CROSS_RATE  as  PaymentCrossRate,
inv.PAYMENT_CURRENCY_CODE  as  PaymentCurrencyCode,
inv.INVOICE_CURRENCY_CODE  as  CurrencyCode,
inv.INVOICE_NUM  as  InvoiceNumber,
inv.CREATION_DATE as OracleCreationDate,
inv.WFAPPROVAL_STATUS as WfApprovalStatus,
lines.attribute4 as CapexCode,
lines.RCV_SHIPMENT_LINE_ID  as  RcvShipmentLineId,
lines.INCLUDED_TAX_AMOUNT  as  IncludedTaxAmount,
lines.PO_DISTRIBUTION_ID  as  PODistributionId,
lines.PO_LINE_LOCATION_ID  as  POLineLocationId,
lines.PO_LINE_ID  as  POLineId,
lines.PO_HEADER_ID  as  POHeaderId,
lines.UNIT_MEAS_LOOKUP_CODE  as  UnitMeasureLookupCode,
lines.QUANTITY_INVOICED  as  QuantityInvoiced,
lines.PERIOD_NAME  as  PeriodName,
lines.ITEM_DESCRIPTION  as  ItemDescription,
lines.INVENTORY_ITEM_ID  as  InventoryItemId,
lines.DESCRIPTION  as  LineDescription,
lines.LINE_TYPE_LOOKUP_CODE  as  LineTypeLookupCode,
lines.LINE_NUMBER  as  LineNumber,
lines.TAX_RATE  as  TaxRate,
lines.AMOUNT     as   LineAmount,
dists.INVOICE_LINE_NUMBER  as  InvoiceLineNumber,
dists.ACCOUNTING_EVENT_ID  as  AccountingEventId,
dists.MATCHED_UOM_LOOKUP_CODE  as  MatchedUomLookupCode,
dists.INVOICE_DISTRIBUTION_ID  as  InvoiceDistributionId,
dists.POSTED_FLAG  as  PostedFlag,
dists.AMOUNT  as  DistributionAmount,
dists.SET_OF_BOOKS_ID  as  LedgerId,
dists.DIST_CODE_COMBINATION_ID  as  DistributionCodeCombinationId,
dists.ACCOUNTING_DATE  as  AccountingDate,
dists.MATCH_STATUS_FLAG as MatchStatusFlag,
gll.NAME as LedgerName,
rates.CONVERSION_RATE  as  ConversionRate,
POH.PONumber,
POH.POCreationDate,
POH.POHeaderAmount,
POH.POConversionRate,
inv_pmt.CheckNumber,
inv_pmt.LastPaymentDate,
hr.NAME as OperatingUnitName,
trm.NAME as PaymentTerms,
coalesce(hrloc.COUNTRY,site.COUNTRY) as LocationCountry,
pm.PAYMENT_METHOD_NAME as PaymentMethodName,
case 
     when hold.TotalCountHold <> hold.CountofReleaseCode then 'Needs Revalidation'
     when count(dists.INVOICE_DISTRIBUTION_ID)  OVER(PARTITION BY dists.INVOICE_ID)
          =  count(case when dists.MATCH_STATUS_FLAG = 'A' then 1 else null end) OVER(PARTITION BY dists.INVOICE_ID) then 'Validated'
     when count(dists.INVOICE_DISTRIBUTION_ID)  OVER(PARTITION BY dists.INVOICE_ID)
          =  count(case when dists.MATCH_STATUS_FLAG = 'N' or null then 1 else null end) OVER(PARTITION BY dists.INVOICE_ID) then 'Never Validated'
     else 'Needs Revalidation' 
     end InvoiceStatus,
dists.LoadBy,
dists.LoadDate,
dists.LoadProcess,
dists.CreateBy, 
dists.CreateDate,
dists.CreateProcess,
dists.UpdateBy,
dists.UpdateDate,
dists.UpdateProcess,
dists.InactiveInd,
dists.InactiveDate,
dists.InactiveReason,
row_number () over(partition by inv.INVOICE_ID order by dists.INVOICE_DISTRIBUTION_ID) as Rank,
row_number () over(partition by POH.PONumber) as RankPOHeader,
dists.DISTRIBUTION_LINE_NUMBER as DistributionLineNumber,
--POH.CapexCode,     
hold.HoldLookupCode,	 
(CASE WHEN hold.HoldLookupCode IS NOT NULL THEN 'Also On Hold' ELSE NULL END) AlsoOnHold,	
(CASE WHEN SUM(IFNULL(dists.AMOUNT,0)) OVER (PARTITION BY dists.INVOICE_ID) = IFNULL (inv.INVOICE_AMOUNT, 0) THEN NULL ELSE 'Invoice <> Distribution' END) InvoiceVsDistribution,
inv_pmt.CheckPaymentStatus,
inv.CREATED_BY CreatedBy,
inv.LAST_UPDATED_BY LastUpdatedBy

from ${ref("OracleEbs-AP_INVOICES_ALL")}  as inv
inner join ${ref("OracleEbs-AP_INVOICE_LINES_ALL")} as lines
on inv.INVOICE_ID = lines.INVOICE_ID  and lines.InactiveInd = false
inner  join  ${ref("OracleEbs-AP_INVOICE_DISTRIBUTIONS_ALL")} as dists
on inv.INVOICE_ID = dists.INVOICE_ID and lines.LINE_NUMBER = dists.INVOICE_LINE_NUMBER and dists.InactiveInd=false
inner join ${ref("OracleEbs-GL_LEDGERS")}  as gll
on dists.SET_OF_BOOKS_ID = gll.LEDGER_ID and gll.InactiveInd=false
left join  ${ref("OracleEbs-GL_DAILY_RATES")} as rates
on date(rates.CONVERSION_DATE) = date(inv.INVOICE_DATE) and rates.FROM_CURRENCY = inv.INVOICE_CURRENCY_CODE
and rates.CONVERSION_TYPE = 'Spot'  and rates.TO_CURRENCY ='USD' and rates.InactiveInd=false
left join POH 
on  inv.QUICK_PO_HEADER_ID =POH.POHeaderId 
left join ${ref("OracleEbs-HR_ALL_ORGANIZATION_UNITS")} as hr
on inv.ORG_ID = hr.ORGANIZATION_ID  and hr.InactiveInd=false
left  join inv_pmt 
on inv.INVOICE_ID = INV_PMT.INVOICE_ID
left join ${ref("OracleEbs-AP_TERMS")} as trm
on inv.terms_id = trm.term_id and trm.InactiveInd=false
left join ${ref("OracleEbs-HR_LOCATIONS_ALL")} hrloc
on lines.SHIP_TO_LOCATION_ID = hrloc.LOCATION_ID and hrloc.InactiveInd=false
left join ${ref("OracleEbs-AP_SUPPLIER_SITES_ALL")} as site
on inv.VENDOR_SITE_ID = site.VENDOR_SITE_ID and site.InactiveInd=false
left join ${ref("OracleEbs-IBY_PAYMENT_METHODS_TL")} pm
on inv.PAYMENT_METHOD_CODE = pm.PAYMENT_METHOD_CODE and pm.LANGUAGE = 'US' and pm.InactiveInd=false
left join hold
on hold.INVOICE_ID = inv.INVOICE_ID